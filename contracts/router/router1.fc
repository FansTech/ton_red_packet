#include "../imports/stdlib.fc";
#include "../lib/export.fc";
#include "../utils/export.fc";

#include "../red_packet/export.fc";

#include "storage1.fc";
#include "request.fc";
#include "op.fc";
#include "getter_fee.fc";
#include "getter_base.fc";
#include "getter.fc";
#include "deploy1.fc";
#include "create.fc";
#include "create_finished.fc";
#include "withdraw.fc";
#include "claim.fc";
#include "close.fc";
#include "refund.fc";
#include "../imports/stdlib_ext.fc";
#include "../red_packet/op.fc";

() @init_procedure(
cell params
) impure inline_ref {

    throw_unless(201, _state:u8 == state::router::initializing);
    throw_unless(202, equal_slices(sender:addr, _router_admin:addr));

    _state:u8 = state::router::normal;

    (
        _reporter:addr,
        _red_packet_base_code:cell,
        _red_packet_deployment:cell,
        _server_public_key:u256,
        _server:addr
    ) = router::Init::de(params);

    save_storage();
}

() @create_procedure(
cell params,
int jetton_amount:u256,
slice from_wallet:addr
) impure inline_ref {

    (
        int red_packet_index:u64, ;;服务器指定
        int red_packet_type:u8, ;;多人平均,多人随机
        int total_pack:u16, ;;总共几个
        int deadline:u32, ;;deadline
        int perfee:u256, ;;给服务器的手续费
        ;;钱包(红包ID, token(wallet),红包金额,红包类型,数量,Perfee,deadline... )
        cell signature:cell
    ) = router::Create::de(params);

    ;;校验
    {
        ;;校验参数
        throw_unless(145, total_pack:u16 > 0);
        throw_unless(146, (red_packet_type:u8 == type::red_packet::multiple_average) | (red_packet_type:u8 == type::red_packet::multiple_random));


        ;;校验签名
        int result = check_signature(
            cell_hash(
                begin_cell()
                ;;这里决定需要被签名保护的内容
                    .store_uint(query_id:u64, 64)
                    .store_uint64(red_packet_index:u64)
                    .store_uint8(red_packet_type:u8)
                    .store_uint16(total_pack:u16)
                    .store_uint32(deadline:u32)
                    .store_uint256(perfee:u256)
                    .store_ref(
                        begin_cell()
                            .store_slice(sender:addr) ;;jetton_user_wallet:addr 也就是router_jetton_wallet
                            .store_uint(jetton_amount:u256, 256) ;;red_packet_supply::u256
                            .store_slice(my_address()) ;;router
                            .end_cell()
                    )
                    .end_cell()
            ),
            signature:cell.begin_parse(),
            _server_public_key:u256
        );
        throw_unless(133, result == -1);
    }

    @create(
        red_packet_index:u64,
        red_packet_type:u8,
        total_pack:u16,
        jetton_amount:u256,
        sender:addr, ;;jetton wallet of router
        from_wallet:addr,
        deadline:u32,
        perfee:u256
    );

    save_storage();
    return ();
}


() @create_finished_procedure(
cell params
) impure inline_ref {

    (
        int red_packet_index:u64,
        int red_packet_type:u8,
        int total_pack:u16,
        slice token:addr,
        int amount:u256,
        slice creator:addr,
        int perfee:u256,
        int deadline:u32
    ) = router::Create_finished::de(params);

    @create_finished(
        red_packet_index:u64,
        red_packet_type:u8,
        total_pack:u16,
        token:addr,
        amount:u256,
        creator:addr,
        perfee:u256,
        deadline:u32
    );

    save_storage();
    return ();
}

;;服务器带领
() @claim_procedure(
cell params
) impure inline_ref {


    ;;check if it is the server
    throw_unless(203, equal_slices(sender:addr, _server:addr));

    @claim(
        params
    );

    save_storage();
    return ();
}

() @withdraw_procedure(
cell params
) impure inline_ref {

    (
        slice token:addr,
        int amount:u256,
        slice recipient:addr,
        cell recipient_uid:cell,
        slice creator:addr,
        int finished:u1,
        int red_packet_index:u64
    ) = router::Withdraw::de(params);

    throw_unless(204,
        equal_slices(
            sender:addr,
            calculate_address(red_packet::state_init(
                _red_packet_base_code:cell,
                _code_manager_public_key:u256,
                my_address(),
                red_packet_index:u64
            ))
        )
    );

    @withdraw(
        token:addr,
        amount:u256,
        recipient:addr,
        recipient_uid:cell,
        creator:addr,
        finished:u1,
        red_packet_index:u64
    );

    save_storage();
    return ();
}

;;用户发起
() @close_procedure(
cell params
) impure inline_ref {

    (
        int red_packet_index:u64,
        slice refund_account:addr,
        int refund_amount:u256,
        cell signature:cell
    ) = router::Close::de(params);

    ;;校验
    {
        ;;校验签名
        int result = check_signature(
            cell_hash(
                begin_cell()
                ;;这里决定需要被签名保护的内容
                    .store_uint(query_id:u64, 64)
                    .store_uint64(red_packet_index:u64)
                    .store_slice(refund_account:addr)
                    .store_uint256(refund_amount:u256)
                    .end_cell()
            ),
            signature:cell.begin_parse(),
            _server_public_key:u256
        );
        throw_unless(133, result == -1);
    }

    @close(
        red_packet_index:u64,
        refund_account:addr,
        refund_amount:u256
    );

    save_storage();
    return ();
}


() @refund_procedure(
cell params
) impure inline_ref {

    (
        slice token:addr,
        int refund_amount:u256,
        slice refund_account:addr,
        int red_packet_index:u64
    ) = router::Refund::de(params);

    throw_unless(204,
        equal_slices(
            sender:addr,
            calculate_address(red_packet::state_init(
                _red_packet_base_code:cell,
                _code_manager_public_key:u256,
                my_address(),
                red_packet_index:u64
            ))
        )
    );

    @refund(
        token:addr,
        refund_amount:u256,
        refund_account:addr,
        red_packet_index:u64
    );

    save_storage();
    return ();
}

() @transfer_notification(slice in_msg_body) impure {

    ;;amount:(VarUInteger 16)
    ;;sender:MsgAddress
    ;;forward_payload:(Either Cell ^Cell)

    int jetton_amount:u256 = in_msg_body~load_coins();
    slice from_wallet:addr = in_msg_body~load_msg_addr();

    int forward_payload_either:u1 = in_msg_body~load_uint(1);

    slice forward_payload:slice = begin_cell().end_cell().begin_parse();
    if (forward_payload_either:u1 == 0) {
        forward_payload:slice = in_msg_body;
    } else {
        forward_payload:slice = in_msg_body~load_ref().begin_parse();
    }

    ;;processing forward payload
    if (forward_payload:slice.slice_bits() < 32) {
        ;;notification without any data, should send back;
        ;;todo
        return ();
    }

    int forward_payload_op:u32 = forward_payload:slice~load_uint(32);
    int forward_payload_query_id:u64 = forward_payload:slice~load_uint(64);

    ~strdump("notification read op");
    ~dump(forward_payload_op:u32);

    if (forward_payload_query_id:u64 == 0) {
        forward_payload_query_id:u64 = query_id:u64;
    }

    ;;either_forward_payload should be sell or init_buy
    if (forward_payload_op:u32 == op::router::create) {
        ~strdump("op::router::create");
        @create_procedure(
            forward_payload:slice~load_ref(),
            jetton_amount:u256,
            from_wallet:addr
        );
        return ();
    }
    ;;if no valid op, ignore, let client ask service to refund fund
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        ;; ignore all bounced messages
        return ();
    }
    if (in_msg_body.slice_empty?()) {
        ;;revert all unknown op
        throw(1010);
    }

    sender:addr = cs~load_msg_addr();

    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
    query_id:u64 = in_msg_body~load_uint(64); ;; we must have query_id
    ~strdump("router read op and query_id");
    ~dump(op);
    ~dump(query_id:u64);
    my_balance:coin = my_balance;
    msg_value:coin = msg_value;

    load_storage();
    if (op == op::router::init) {
        ~strdump("op::router::init");
        @init_procedure(in_msg_body~load_ref());
        return ();
    }
    if (op == op::router::claim) {
        ;;领红包
        ~strdump("op::router::claim");
        @claim_procedure(in_msg_body~load_ref());
        return ();
    }
    if (op == op::router::withdraw) {
        ;;领红包打款调用
        ~strdump("op::router::withdraw");
        @withdraw_procedure(in_msg_body~load_ref());
        return ();
    }
    if (op == op::router::close) {
        ;;红包退款
        ~strdump("op::router::close");
        @close_procedure(in_msg_body~load_ref());
        return ();
    }
    if (op == op::router::refund) {
        ;;红包退款打款调用
        ~strdump("op::router::refund");
        @refund_procedure(in_msg_body~load_ref());
        return ();
    }
    if (op == op::router::create_finished) {
        ;;新建红包反馈调用
        ~strdump("op::router::create_finished");
        @create_finished_procedure(in_msg_body~load_ref());
        return ();
    }
    if (op == op::transfer_notification()) {
        ~strdump("op::transfer_notification");
        ;;op::router::create, 创建红包
        @transfer_notification(in_msg_body);
        return ();
    }
    ;;====
    if (op == op::router::deploy) {
        ~strdump("op::router::deploy");
        @deploy_procedure(in_msg_body~load_ref());
        return ();
    }

    throw(1007); ;; if the message contains an op that is not known to this contract, we throw
}
